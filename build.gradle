plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.4.0'

    id 'io.gitlab.arturbosch.detekt' version '1.11.0'
    id 'jacoco'
    id 'de.jansauer.printcoverage' version '2.0.0'

    id 'net.researchgate.release' version '2.8.1'

    id 'org.jetbrains.dokka' version '0.10.1'
    id 'com.github.hierynomus.license' version '0.15.0'

    id 'maven'
    id 'maven-publish'
}

sourceSets {
    main {
        kotlin {
            srcDir 'src'
        }
        resources {
            srcDir 'resources'
        }
    }
    test {
        kotlin {
            srcDir 'test'
        }
        resources {
            srcDir 'test-resources'
        }
    }
}

compileKotlin {
    kotlinOptions {
        freeCompilerArgs = ['-Xno-param-assertions', '-Xno-call-assertions']
        kotlinOptions.jvmTarget = '1.6'
    }
}

compileTestKotlin {
    kotlinOptions.jvmTarget = '1.6'
}

repositories {
    maven { url nexusUrl }
}

dependencies {
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlinxVersion"

    implementation "org.slf4j:slf4j-api:$slf4jVersion"

    testImplementation "ch.qos.logback:logback-classic:$logbackVersion"

    testImplementation "org.junit.jupiter:junit-jupiter-api:$junitJupiterVersion"
    testRuntime "org.junit.jupiter:junit-jupiter-engine:$junitJupiterVersion"
}

detekt {
    input = files('src')
    config = files('detekt.yml')

    reports {
        xml {
            enabled = true
            destination = file("$buildDir/reports/detekt.xml")
        }
    }
}

license {
    exclude '**/*.json'
    exclude '**/*.properties'
    exclude '**/META-INF/**/*'

    header = file('HEADER.txt')

    ext {
        year = Calendar.getInstance().get(Calendar.YEAR)
    }
}

jacoco {
    toolVersion = '0.8.5'
}

test {
    useJUnitPlatform()
    jacoco {
        destinationFile = file("$buildDir/jacoco/test.exec")
    }
}

jacocoTestReport.dependsOn('check')
build.dependsOn('jacocoTestReport')

release {
    tagTemplate = 'v$version'
}

dokka {
    configuration {
        moduleName = 'horus-events'

        outputFormat = 'html'
        outputDirectory = "$buildDir/javadoc"

        jdkVersion = 6

        impliedPlatforms = ["JVM"]

        noStdlibLink = false
    }
}

task packageSources(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task packageJavadoc(type: Jar, dependsOn: dokka) {
    classifier = 'javadoc'
    from "$buildDir/javadoc"
}

artifacts.archives packageSources, packageJavadoc

publishing {
    publications {
        MavenPublication(MavenPublication) {
            from components.java
            groupId project.group
            artifactId project.name
            version project.version

            artifact packageSources {
                classifier 'sources'
            }

            artifact packageJavadoc {
                classifier 'javadoc'
            }
        }
    }
}

publishing {
    publications {
        mavenProject(MavenPublication) {
            from components.java
            groupId project.group
            artifactId project.name
            version project.version

            artifact packageSources {
                classifier 'sources'
            }

            artifact packageJavadoc {
                classifier 'javadoc'
            }
        }
    }

    def nexusUser = project.hasProperty('nexusUser') ? project.property('nexusUser') : System.getenv('NEXUS_USER')
    def nexusPass = project.hasProperty('nexusPass') ? project.property('nexusPass') : System.getenv('NEXUS_PASS')

    if (nexusUser != null && nexusPass != null) {
        repositories {
            maven {
                name 'vemilyus-nexus'

                if (project.version.endsWith('-SNAPSHOT'))
                    url 'https://nexus.vemilyus.com/repository/snapshots/'
                else
                    url 'https://nexus.vemilyus.com/repository/releases/'

                credentials(PasswordCredentials) {
                    username nexusUser
                    password nexusPass
                }
            }
        }
    }
}
